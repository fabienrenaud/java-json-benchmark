plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id "com.github.ben-manes.versions" version '0.28.0'
}

group = 'com.github.fabienrenaud'
version = '7'
mainClassName = 'com.github.fabienrenaud.jjb.Cli'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

ext {
    jacksonVersion = '2.10.2'
    dslJsonVersion = '1.9.5'
    johnzonVersion = '1.2.3'
    jmhVersion = '1.23'
}

dependencies {

    // CLI and misc
    compile group: 'io.airlift', name: 'airline', version: '0.9'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'

    // org.json
    compile group: 'org.json', name: 'json', version: '20090211'
    // Jackson
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "${jacksonVersion}"
    compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-afterburner', version: "${jacksonVersion}"
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: "${jacksonVersion}"
    // GSON
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    // JSONP
    compile group: 'javax.json', name: 'javax.json-api', version: '1.1.4'
    compile group: 'org.glassfish', name: 'javax.json', version: '1.1.4'
    // JSONB
    compile group: 'javax.json.bind', name: 'javax.json.bind-api', version: '1.0'
    compile group: 'org.eclipse', name: 'yasson', version: '1.0.6'
    // GENSON
    compile group: 'com.owlike', name: 'genson', version: '1.6'
    // FlexJson
    compile group: 'net.sf.flexjson', name: 'flexjson', version: '3.3'
    // FastJson
    compile group: 'com.alibaba', name: 'fastjson', version: '1.2.62'
    // json-io
    compile group: 'com.cedarsoftware', name: 'json-io', version: '4.12.0'
    // boon
    compile group: 'io.fastjson', name: 'boon', version: '0.34'
    // johnzon
    compile group: 'org.apache.johnzon', name: 'johnzon-core', version: "${johnzonVersion}"
    compile group: 'org.apache.johnzon', name: 'johnzon-mapper', version: "${johnzonVersion}"
    // json-smart
    compile group: 'net.minidev', name: 'json-smart', version: '2.3'
    // DSL-json
    compile group: 'com.dslplatform', name: 'dsl-json-java8', version: "${dslJsonVersion}"
    annotationProcessor group: 'com.dslplatform', name: 'dsl-json-java8', version: "${dslJsonVersion}"
    // LoganSquare
    compile group: 'com.bluelinelabs', name: 'logansquare', version: '1.3.7'
    annotationProcessor group: 'com.bluelinelabs', name: 'logansquare-compiler', version: '1.3.7'
    // json-simple
    compile group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    // nanojson
    compile group: 'com.grack', name: 'nanojson', version: '1.4'
    // jodd
    compile group: 'org.jodd', name: 'jodd-json', version: '5.1.3'
    // moshi
    compile group: 'com.squareup.moshi', name: 'moshi', version: '1.9.2'
    // tapestry
    compile group: 'org.apache.tapestry', name: 'tapestry-json', version: '5.4.5'
    // jsoniter
    compile group: 'com.jsoniter', name: 'jsoniter', version: '0.9.23'
    compile group: 'org.javassist', name: 'javassist', version: '3.26.0-GA'
    // minimal-json
    compile group: 'com.eclipsesource.minimal-json', name: 'minimal-json', version: '0.9.5'
    // mjson
    compile group: 'org.sharegov', name: 'mjson', version: '1.4.1'
    // underscore-java
    compile group: 'com.github.javadev', name: 'underscore', version: '1.52'
    //purejson 
    compile group: 'io.github.senthilganeshs', name: 'purejson', version: '1.0.1'

    // Test
    testCompile group: 'junit', name: 'junit', version: '4.13'

    // IMPORTANT: Leave JMH at the end!
    // JMH
    compile group: 'org.openjdk.jmh', name: 'jmh-core', version: "${jmhVersion}"
    annotationProcessor group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess', version: "${jmhVersion}"
}

shadowJar {
    archiveName = 'app.jar'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}

task (depsize) {
    doLast {
        def size = 0;
        def formatStr = "%,10.2f"
        configurations.default.collect { it.length() / (1024 * 1024) }.each { size += it }

        def out = new StringBuffer()
        out << 'Total dependencies size:'.padRight(45)
        out << "${String.format(formatStr, size)} MiB\n\n"

        configurations
            .default
            .sort { -it.length() }
            .each {
                out << "${it.name}".padRight(45)
                out << "${String.format(formatStr, (it.length() / 1024))} KiB\n"
            }
            println(out)
    }
}

