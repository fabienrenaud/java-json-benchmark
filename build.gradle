plugins {
    id 'java'
    id 'application'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id "com.github.ben-manes.versions" version '0.28.0'
}

group = 'com.github.fabienrenaud'
version = '7'
mainClassName = 'com.github.fabienrenaud.jjb.Cli'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

ext {
    jacksonVersion = '2.10.2'
    dslJsonVersion = '1.9.5'
    johnzonVersion = '1.2.3'
    jmhVersion = '1.23'
}

dependencies {

    // CLI and misc
    implementation group: 'io.airlift', name: 'airline', version: '0.9'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'

    // org.json
    implementation group: 'org.json', name: 'json', version: '20090211'
    // Jackson
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "${jacksonVersion}"
    implementation group: 'com.fasterxml.jackson.module', name: 'jackson-module-afterburner', version: "${jacksonVersion}"
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: "${jacksonVersion}"
    // GSON
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    // JSONP
    implementation group: 'javax.json', name: 'javax.json-api', version: '1.1.4'
    implementation group: 'org.glassfish', name: 'javax.json', version: '1.1.4'
    // JSONB
    implementation group: 'javax.json.bind', name: 'javax.json.bind-api', version: '1.0'
    implementation group: 'org.eclipse', name: 'yasson', version: '1.0.6'
    // GENSON
    implementation group: 'com.owlike', name: 'genson', version: '1.6'
    // FlexJson
    implementation group: 'net.sf.flexjson', name: 'flexjson', version: '3.3'
    // FastJson
    implementation group: 'com.alibaba', name: 'fastjson', version: '1.2.62'
    // json-io
    implementation group: 'com.cedarsoftware', name: 'json-io', version: '4.12.0'
    // boon
    implementation group: 'io.fastjson', name: 'boon', version: '0.34'
    // johnzon
    implementation group: 'org.apache.johnzon', name: 'johnzon-core', version: "${johnzonVersion}"
    implementation group: 'org.apache.johnzon', name: 'johnzon-mapper', version: "${johnzonVersion}"
    // json-smart
    implementation group: 'net.minidev', name: 'json-smart', version: '2.3'
    // DSL-json
    implementation group: 'com.dslplatform', name: 'dsl-json-java8', version: "${dslJsonVersion}"
    annotationProcessor group: 'com.dslplatform', name: 'dsl-json-java8', version: "${dslJsonVersion}"
    // LoganSquare
    implementation group: 'com.bluelinelabs', name: 'logansquare', version: '1.3.7'
    annotationProcessor group: 'com.bluelinelabs', name: 'logansquare-compiler', version: '1.3.7'
    // json-simple
    implementation group: 'com.googlecode.json-simple', name: 'json-simple', version: '1.1.1'
    // nanojson
    implementation group: 'com.grack', name: 'nanojson', version: '1.4'
    // jodd
    implementation group: 'org.jodd', name: 'jodd-json', version: '5.1.3'
    // moshi
    implementation group: 'com.squareup.moshi', name: 'moshi', version: '1.9.2'
    // tapestry
    implementation group: 'org.apache.tapestry', name: 'tapestry-json', version: '5.4.5'
    // jsoniter
    implementation group: 'com.jsoniter', name: 'jsoniter', version: '0.9.23'
    implementation group: 'org.javassist', name: 'javassist', version: '3.26.0-GA'
    // minimal-json
    implementation group: 'com.eclipsesource.minimal-json', name: 'minimal-json', version: '0.9.5'
    // mjson
    implementation group: 'org.sharegov', name: 'mjson', version: '1.4.1'
    // underscore-java
    implementation group: 'com.github.javadev', name: 'underscore', version: '1.52'
    //purejson 
    implementation group: 'io.github.senthilganeshs', name: 'purejson', version: '1.0.1'

    // Test
    testImplementation group: 'junit', name: 'junit', version: '4.13'

    // IMPORTANT: Leave JMH at the end!
    // JMH
    implementation group: 'org.openjdk.jmh', name: 'jmh-core', version: "${jmhVersion}"
    annotationProcessor group: 'org.openjdk.jmh', name: 'jmh-generator-annprocess', version: "${jmhVersion}"
}

shadowJar {
    archiveName = 'app.jar'
    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

test {
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}

task (depsize) {
    doLast {
        def size = 0;
        def formatStr = "%,10.2f"
        configurations.default.collect { it.length() / (1024 * 1024) }.each { size += it }

        def out = new StringBuffer()
        out << 'Total dependencies size:'.padRight(45)
        out << "${String.format(formatStr, size)} MiB\n\n"

        configurations
            .default
            .sort { -it.length() }
            .each {
                out << "${it.name}".padRight(45)
                out << "${String.format(formatStr, (it.length() / 1024))} KiB\n"
            }
            println(out)
    }
}

